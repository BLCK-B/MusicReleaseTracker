plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id "org.openjfx.javafxplugin" version "0.0.14"
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'com.blck'
version '3.0.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.3'
}

configurations {
    runtime
    compile
}

sourceCompatibility = '19'
targetCompatibility = '19'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.blck.MusicReleaseTracker'
    mainClass = 'com.blck.MusicReleaseTracker.Main'
}

javafx {
    version = '20.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'com.typesafe:config:1.4.2'
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'MusicReleaseTracker'
    }
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

//This will create an executable JAR file with a manifest that specifies the main class
tasks.jar {
    manifest {
        attributes 'Main-Class': 'com.blck.MusicReleaseTracker.Main'
    }
    duplicatesStrategy = 'EXCLUDE'
    from(sourceSets.main.output) {
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    }
    archiveFileName = "MusicReleaseTracker.jar"
    dependsOn configurations.runtimeClasspath
}

shadowJar {
    exclude 'org/sqlite/native/Linux-Musl/**'
    exclude 'org/sqlite/native/Linux-Android/**'
    exclude 'org/sqlite/native/FreeBSD/**'
    exclude 'org/sqlite/native/Mac/**'
    exclude 'org/sqlite/native/Linux/aarch64/**'
    exclude 'org/sqlite/native/Linux/arm/**'
    exclude 'org/sqlite/native/Linux/armv6/**'
    exclude 'org/sqlite/native/Linux/ppc64/**'
    exclude 'org/sqlite/native/Windows/aarch64/**'
    configurations = [project.configurations.runtimeClasspath, project.configurations.shadow]
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'com.blck.MusicReleaseTracker.Main'
    }
    mergeServiceFiles()
}

run {
    systemProperty "ide.environment", "IDE"
}