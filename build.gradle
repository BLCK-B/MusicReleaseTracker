/*
 *         MusicReleaseTracker
 *         Copyright (C) 2023 - 2024 BLCK
 *         This program is free software: you can redistribute it and/or modify
 *         it under the terms of the GNU General Public License as published by
 *         the Free Software Foundation, either version 3 of the License, or
 *         (at your option) any later version.
 *         This program is distributed in the hope that it will be useful,
 *         but WITHOUT ANY WARRANTY; without even the implied warranty of
 *         MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *         GNU General Public License for more details.
 *         You should have received a copy of the GNU General Public License
 *         along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

// https://services.gradle.org/distributions - in gradle-wrapper.properties

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.4'
}

group 'com.blck'
def applicationProperties = file('src/main/resources/application.properties')
def properties = new Properties()
applicationProperties.withInputStream { stream ->
    properties.load(stream)
}
version = properties.getProperty('app.version')

repositories {
    mavenCentral()
    gradlePluginPortal()
}

graalvmNative {
    binaries.all {
        imageName.set("MusicReleaseTracker")
        buildArgs.add('--verbose')
        // https://graalvm.org/dev/reference-manual/native-image/optimizations-and-performance
        buildArgs.add('-O1')
//        buildArgs.add('-Ob')
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.48.0.0'
    implementation 'org.jsoup:jsoup:1.18.3'

    testImplementation(platform('org.junit:junit-bom:5.11.4'))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation 'org.mockito:mockito-core:5.15.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.15.2'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

bootJar {
    duplicatesStrategy = 'exclude'
    archiveFileName = "MRT-${version}.jar"
}

// generate aot tracing - rather see contributing.md
//bootRun {
//    jvmArgs("-agentlib:native-image-agent=config-output-dir=tracing")
//}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

tasks.jar {
    manifest {
        attributes 'Main-Class': 'com.blck.MusicReleaseTracker.Main'
    }
    duplicatesStrategy = 'exclude'
    from(sourceSets.main.output) {
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    }
    dependsOn configurations.runtimeClasspath
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

